Kind origin type.
Kind loan type.
Kind point type.

/* input */
Type origin_live_on_entry origin -> point -> prop.
Type loan_issued_at origin -> loan -> point -> prop.
Type cfg_edge point -> point -> prop.
Type loan_invalidated_at loan -> point -> prop.
Type not_loan_killed_at loan -> point -> prop.
Type subset_base origin -> origin -> point -> prop.

/* At any point, the origin must either be live or dead */
Theorem OriginLiveAxiom: 
  forall Origin,
  forall Point,
  (origin_live_on_entry Origin Point ) \/ ( origin_live_on_entry Origin Point -> false).
skip.

Define dead_borrow_region_can_reach_root: origin -> point -> loan -> prop ,
       dead_borrow_region_can_reach_dead: origin -> point -> loan -> prop,
       dying_region_requires: origin -> point -> point -> loan -> prop ,
       live_to_dying_regions: origin -> origin -> point -> point -> prop ,
       dying_can_reach_origins: origin -> point -> point -> prop ,
       dying_can_reach: origin  -> origin -> point -> point -> prop,
       dying_can_reach_live: origin -> origin -> point -> point -> prop ,
       datafrog_opt_subset: origin -> origin -> point -> prop,
       datafrog_opt_origin_contains_loan_on_entry: origin -> loan -> point -> prop,
       datafrog_opt_loan_live_at: loan -> point -> prop,
       datafrog_opt_errors: loan -> point -> prop by

dead_borrow_region_can_reach_root Origin Point Loan :=
  loan_issued_at Origin  Loan  Point /\
  (origin_live_on_entry Origin  Point -> false) ;

dead_borrow_region_can_reach_dead Origin Point Loan :=
  dead_borrow_region_can_reach_root Origin Point Loan;

dead_borrow_region_can_reach_dead  Origin2  Point  Loan  :=
  exists Origin1,
  dead_borrow_region_can_reach_dead Origin1  Point  Loan  /\
  datafrog_opt_subset Origin1  Origin2  Point  /\
  ( origin_live_on_entry Origin2  Point -> false );

dying_region_requires Origin  Point1  Point2  Loan  :=
  datafrog_opt_origin_contains_loan_on_entry Origin  Loan  Point1   /\
  not_loan_killed_at Loan  Point1   /\
  cfg_edge Point1  Point2  /\
  (origin_live_on_entry Origin  Point2 -> false);

live_to_dying_regions Origin1  Origin2  Point1  Point2  :=
  datafrog_opt_subset Origin1  Origin2  Point1  /\
  cfg_edge Point1  Point2  /\
  origin_live_on_entry Origin1  Point2  /\
  (origin_live_on_entry Origin2  Point2 -> false ) ;

dying_can_reach_origins Origin2  Point1  Point2  :=
  exists Origin1,
  live_to_dying_regions Origin1  Origin2  Point1  Point2 ;

dying_can_reach_origins Origin  Point1  Point2  :=
  exists Loan,
  dying_region_requires Origin  Point1  Point2  Loan ;

dying_can_reach Origin1  Origin2  Point1  Point2  :=
  dying_can_reach_origins Origin1  Point1  Point2 /\ 
  datafrog_opt_subset Origin1  Origin2  Point1 ;

dying_can_reach Origin1  Origin3  Point1  Point2  :=
  exists Origin2,
  dying_can_reach Origin1  Origin2  Point1  Point2   /\
  ( origin_live_on_entry Origin2  Point2 -> false ) /\ 
  datafrog_opt_subset Origin2  Origin3  Point1 ;

dying_can_reach_live Origin1  Origin2  Point1  Point2  :=
  dying_can_reach Origin1  Origin2  Point1  Point2   /\
  origin_live_on_entry Origin2  Point2 ;

datafrog_opt_subset Origin1  Origin2  Point  :=
  subset_base Origin1  Origin2  Point ;

datafrog_opt_subset Origin1  Origin2  Point2  :=
  exists Point1,
  datafrog_opt_subset Origin1  Origin2  Point1   /\
  cfg_edge Point1  Point2  /\
  origin_live_on_entry Origin1  Point2   /\
  origin_live_on_entry Origin2  Point2 ;

datafrog_opt_subset Origin1  Origin3  Point2  :=
  exists Point1,
  exists Origin2,
  live_to_dying_regions Origin1  Origin2  Point1  Point2  /\
  dying_can_reach_live Origin2  Origin3  Point1  Point2 ;

datafrog_opt_origin_contains_loan_on_entry Origin  Loan  Point  :=
  loan_issued_at Origin  Loan  Point ;

datafrog_opt_origin_contains_loan_on_entry Origin  Loan  Point2  :=
  exists Point1,
  datafrog_opt_origin_contains_loan_on_entry Origin  Loan  Point1  /\
  not_loan_killed_at Loan  Point1  /\
  cfg_edge Point1  Point2  /\
  origin_live_on_entry Origin  Point2 ;

datafrog_opt_origin_contains_loan_on_entry Origin2  Loan  Point2  :=
  exists Point1,
  exists Origin1,
  dying_region_requires Origin1  Point1  Point2  Loan  /\
  dying_can_reach_live Origin1  Origin2  Point1  Point2 ;

datafrog_opt_loan_live_at Loan  Point  :=
  exists Origin,
  datafrog_opt_origin_contains_loan_on_entry Origin  Loan  Point  /\
  origin_live_on_entry Origin  Point ;

datafrog_opt_loan_live_at Loan  Point  :=
  exists Origin1,
  exists Origin2,
  dead_borrow_region_can_reach_dead Origin1  Point  Loan  /\
  datafrog_opt_subset Origin1  Origin2  Point  /\
  origin_live_on_entry Origin2  Point ;

datafrog_opt_errors Loan  Point  :=
  loan_invalidated_at Loan  Point  /\
  datafrog_opt_loan_live_at Loan  Point .




Define naive_subset: origin -> origin -> point -> prop by

naive_subset Origin1  Origin2  Point  :=
  subset_base Origin1  Origin2  Point ;

naive_subset Origin1  Origin2  Point2  :=
  exists Point1,
  naive_subset Origin1  Origin2  Point1 /\
  cfg_edge Point1  Point2 /\
  origin_live_on_entry Origin1  Point2 /\
  origin_live_on_entry Origin2  Point2 ;

naive_subset Origin1  Origin3  Point  :=
  exists Origin2,
  naive_subset Origin1  Origin2  Point /\
  naive_subset Origin2  Origin3  Point .

Define naive_origin_contains_loan_on_entry: origin -> loan -> point -> prop by

naive_origin_contains_loan_on_entry Origin Loan Point  :=
  loan_issued_at Origin Loan Point ;

/* naive_origin_contains_loan_on_entry Origin Loan Point  := */
  /* cfg_node Point   */
  /* placeholder_Loan Origin  Loan ; */

naive_origin_contains_loan_on_entry Origin2  Loan  Point  :=
  exists Origin1,
  naive_origin_contains_loan_on_entry Origin1  Loan  Point /\
  naive_subset Origin1  Origin2  Point ;

naive_origin_contains_loan_on_entry Origin  Loan  Point2  :=
  exists Point1,
  naive_origin_contains_loan_on_entry Origin  Loan  Point1 /\
  not_loan_killed_at Loan  Point1  /\
  cfg_edge Point1  Point2 /\
  origin_live_on_entry Origin  Point2 .

Define naive_loan_live_at: loan -> point -> prop by
naive_loan_live_at Loan  Point  :=
  exists Origin,
  naive_origin_contains_loan_on_entry Origin  Loan  Point /\
  origin_live_on_entry Origin  Point .

Define naive_errors: loan -> point -> prop  by
naive_errors Loan  Point  :=
  loan_invalidated_at Loan  Point /\
  naive_loan_live_at Loan  Point .


Theorem Lemma24:
  ( 
    forall Point, 
    forall Origin1, 
    forall Origin9, 
    datafrog_opt_subset Origin1 Origin9 Point -> 
    naive_subset Origin1 Origin9 Point 
  ) /\ (
    forall Point1, 
    forall Point2, 
    forall Origin1, 
    forall Origin9, 
    dying_can_reach Origin1 Origin9 Point1 Point2  -> 
    naive_subset Origin1 Origin9 Point1
  ).
induction on 1 1.
split.
intros.
case H1.
search.
apply IH to H2.
search.
case H2.
case H3.
apply IH to H4.
apply IH1 to H8.
search.

intros.
case H1.
apply IH to H3.
search.
apply IH1 to H2.
apply IH to H4.
search.


Theorem Lemma26:
  forall Origin,
  forall Loan,
  forall Point,
  datafrog_opt_origin_contains_loan_on_entry Origin Loan Point  ->
  naive_origin_contains_loan_on_entry Origin Loan Point .
induction on 1.
intros.
case H1.
search.
apply IH to H2.
search.
case H2.
apply IH to H4.
case H3.
case Lemma24.
apply H12 to H9.
search.


Theorem Lemma51:
  forall Origin,
  forall Loan,
  forall Point,
  dead_borrow_region_can_reach_dead Origin Point Loan ->
  naive_origin_contains_loan_on_entry Origin Loan Point.
induction on 1.
intros.
case H1.
case H2.
search.
apply IH to H2.
case Lemma24.
apply H6 to H3.
search.


Theorem DatafrogOpt2Naive:
  forall Loan,
  forall Point,
  datafrog_opt_errors Loan Point ->
  naive_errors Loan Point.
intros.
case H1.
case H3.
apply Lemma26 to H4.
search.
apply Lemma51 to H4.
case Lemma24.
apply H8 to H5.
search.


Define  my_subset: origin -> origin -> point -> prop,
        my_origin_contains_loan_on_entry: origin -> loan -> point -> prop by

my_subset Origin1  Origin2  Point  :=
  datafrog_opt_subset Origin1  Origin2  Point ;

my_subset Origin1  Origin3  Point  :=
  exists Origin2,
  datafrog_opt_subset Origin1  Origin2  Point /\
  my_subset Origin2  Origin3  Point ;

my_origin_contains_loan_on_entry Origin Loan Point  :=
  datafrog_opt_origin_contains_loan_on_entry Origin Loan Point ;

/* my_origin_contains_loan_on_entry Origin Loan Point  := */
  /* cfg_node Point   */
  /* placeholder_Loan Origin  Loan ; */

my_origin_contains_loan_on_entry Origin2  Loan  Point  :=
  exists Origin1,
  my_origin_contains_loan_on_entry Origin1  Loan  Point /\
  datafrog_opt_subset Origin1  Origin2  Point .


Theorem Lemma92:
  forall Origin1,
  forall Origin2,
  forall Origin9,
  forall Point2,
  forall Point3,
  dying_can_reach Origin1 Origin2 Point2 Point3 ->
  my_subset Origin2 Origin9 Point2 ->
  origin_live_on_entry Origin9 Point3 ->
  (
    dying_can_reach_live Origin1 Origin9 Point2 Point3 \/
    (
      exists Origin5,
      dying_can_reach_live Origin1 Origin5 Point2 Point3 /\
      origin_live_on_entry Origin5 Point3 /\
      my_subset Origin5 Origin9 Point2
    )
  ).
induction on 2.
intros.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point3.
case H4.
search.
case H2.
search.
assert dying_can_reach Origin1 Origin3 Point2 Point3.
apply IH to H8 H7 H3.
search.


Theorem Lemma90:
  forall Loan,
  forall Origin1,
  forall Origin9,
  forall Point2,
  forall Point3,
  dying_region_requires Origin1 Point2 Point3 Loan ->
  my_subset Origin1 Origin9 Point2 ->
  origin_live_on_entry Origin9 Point3 ->
  (
    datafrog_opt_origin_contains_loan_on_entry Origin9 Loan Point3 \/
    (
      exists Origin5,
      datafrog_opt_origin_contains_loan_on_entry Origin5 Loan Point3 /\
      origin_live_on_entry Origin5 Point3 /\
      my_subset Origin5 Origin9 Point2
    )
  ).
intros.
assert dying_can_reach_origins Origin1 Point2 Point3.
case H2.
search.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point3.
case H7.
search.
assert dying_can_reach Origin1 Origin2 Point2 Point3.
apply Lemma92 to H9 H6 H3.
case H10.
search.
search.


Theorem MySubsetConcat:
  forall Origin1,
  forall Origin2,
  forall Origin3,
  forall Point,
  my_subset Origin1 Origin2 Point ->
  my_subset Origin2 Origin3 Point ->
  my_subset Origin1 Origin3 Point. 
induction on 1.
intros.
case H1.
search.
apply IH to H4 H2.
search.


Theorem MySubsetPoint:
(
  forall Origin0,
  forall Origin9,
  forall Point1,
  forall Point2,
  cfg_edge Point1 Point2 ->
  my_subset Origin0 Origin9 Point1 ->
  origin_live_on_entry Origin0 Point2 ->
  origin_live_on_entry Origin9 Point2 ->
  my_subset Origin0 Origin9 Point2
) /\ (
  forall Origin0,
  forall Origin1,
  forall Origin2,
  forall Origin9,
  forall Point1,
  forall Point2,
  cfg_edge Point1 Point2 ->
  live_to_dying_regions Origin0 Origin1 Point1 Point2 ->
  dying_can_reach Origin1 Origin2 Point1 Point2 ->
  my_subset Origin2 Origin9 Point1 ->
  origin_live_on_entry Origin9 Point2 ->
  my_subset Origin0 Origin9 Point2
) /\ (
  forall Origin0,
  forall Origin1,
  forall Origin9,
  forall Point1,
  forall Point2,
  cfg_edge Point1 Point2 ->
  live_to_dying_regions Origin0 Origin1 Point1 Point2 ->
  my_subset Origin1 Origin9 Point1 ->
  origin_live_on_entry Origin9 Point2 ->
  my_subset Origin0 Origin9 Point2
).
induction on 2 4 3.
split.
intros.
case H2.
search.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point2.
case H7.
apply IH to H1 H6 H8 H4.
search.
case H6.
assert live_to_dying_regions  Origin0 Origin2 Point1 Point2.
assert dying_can_reach Origin2 Origin9 Point1 Point2.
assert dying_can_reach_live Origin2 Origin9 Point1 Point2.
search.
assert live_to_dying_regions  Origin0 Origin2 Point1 Point2.
assert dying_can_reach Origin2 Origin1 Point1 Point2.
apply IH1 to H1 H11 H12 H10 H4.
search.
intros.
case H4.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point2.
case H7.
search.
search.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point2.
case H8.
apply OriginLiveAxiom with Origin = Origin3 , Point = Point2.
case H10.
apply IH to H1 H7 H11 H5.
search.
assert live_to_dying_regions Origin2  Origin3 Point1 Point2.
apply IH2 to H1 H12 H7 H5.
search.
assert dying_can_reach Origin1 Origin3 Point1 Point2.
apply IH1 to H1 H2 H10 H7 H5.
search.
intros.
case H3.
search.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point2.
case H7.
apply IH to H1 H6 H8 H4.
search.
assert dying_can_reach Origin1 Origin2 Point1 Point2.
apply IH1 to H1 H2 H9 H6 H4.
search.


Theorem SubsetNaive2My:
  forall Point2,
  forall Origin1,
  forall Origin9,
  naive_subset Origin1 Origin9 Point2 ->
  my_subset Origin1 Origin9 Point2.
induction on 1.
intros.
case H1.
search.
apply IH to H2.
case H6.
search.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point2.
case H9.
case MySubsetPoint.
apply H11 to H3 H8 H10 H5.
search.
assert live_to_dying_regions Origin1 Origin2 Point1 Point2.
case MySubsetPoint.
apply H14 to H3 H11 H8 H5.
search.
apply IH to H2.
apply IH to H3.
apply MySubsetConcat to H4 H5.
search.


Theorem MyOriginConcat:
  forall Origin1,
  forall Origin9,
  forall Point,
  forall Loan,
  my_origin_contains_loan_on_entry Origin1 Loan Point ->
  my_subset Origin1 Origin9 Point ->
  my_origin_contains_loan_on_entry Origin9 Loan Point. 
induction on 2.
intros.
case H2.
search.
assert my_origin_contains_loan_on_entry Origin2 Loan Point.
apply IH to H5 H4.
search.


Theorem MyOriginPoint:
(
  forall Point1,
  forall Point2,
  forall Loan,
  forall Origin9,
  cfg_edge Point1 Point2 ->
  my_origin_contains_loan_on_entry Origin9 Loan Point1 ->
  origin_live_on_entry Origin9 Point2 ->
  not_loan_killed_at Loan  Point1  ->
  my_origin_contains_loan_on_entry Origin9 Loan Point2
) /\ (
  forall Point1,
  forall Point2,
  forall Loan,
  forall Origin2,
  forall Origin9,
  cfg_edge Point1 Point2 ->
  my_subset Origin2 Origin9 Point1 ->
  my_origin_contains_loan_on_entry Origin2 Loan Point1 ->
  origin_live_on_entry Origin9 Point2 ->
  ( origin_live_on_entry Origin2 Point2 -> false ) ->
  not_loan_killed_at Loan  Point1  ->
  my_origin_contains_loan_on_entry Origin9 Loan Point2
).
induction on 2 3.
split.
intros.
case H2.
search.
apply OriginLiveAxiom with Origin = Origin1 , Point = Point2.
assert my_subset Origin1 Origin9 Point1.
case H7.
apply IH to H1 H5 H9 H4.
search.
apply IH1 to H1 H8 H5 H3 H9 H4.
search.
intros.
case H3.
assert dying_region_requires Origin2 Point1 Point2 Loan.
apply Lemma90 to H8 H2 H4.
case H9.
search.
case MySubsetPoint.
apply H13 to H1 H12 H11 H4.
assert my_origin_contains_loan_on_entry Origin5 Loan Point2.
apply MyOriginConcat to H17 H16.
search.
apply OriginLiveAxiom with Origin = Origin1 , Point = Point2.
case H9.
assert my_subset Origin1 Origin9 Point1.
case MySubsetPoint.
apply H12 to H1 H11 H10 H4.
apply IH to H1 H7 H10 H6.
apply MyOriginConcat to H16 H15.
search.
assert my_subset Origin1 Origin9 Point1.
apply IH1 to H1 H11 H7 H4 H10 H6.
search.


Theorem OriginNaive2My:
  forall Point2,
  forall Origin9,
  forall Loan,
  naive_origin_contains_loan_on_entry Origin9 Loan Point2 ->
  my_origin_contains_loan_on_entry Origin9 Loan Point2.
induction on 1.
intros.
case H1.
search.
apply IH to H2.
apply SubsetNaive2My to H3.
apply MyOriginConcat to H4 H5.
search.
apply IH to H2.
case MyOriginPoint.
apply H7 to H4 H6 H5 H3.
search.


Theorem Lemma97:
  forall Point,
  forall Loan,
  forall Origin1,
  forall Origin9,
  dead_borrow_region_can_reach_dead Origin1 Point Loan ->
  my_subset Origin1 Origin9 Point ->
  origin_live_on_entry Origin9 Point ->
  datafrog_opt_loan_live_at Loan Point.
induction on 2.
intros.
case H2.
search.
apply OriginLiveAxiom with Origin = Origin2 , Point = Point.
case H6.
search.
assert dead_borrow_region_can_reach_dead Origin2 Point Loan.
apply IH to H8 H5 H3.
search.


Theorem Lemma98:
  forall Point,
  forall Loan,
  forall Origin1,
  forall Origin9,
  loan_issued_at Origin1 Loan Point ->
  my_subset Origin1 Origin9 Point ->
  origin_live_on_entry Origin9 Point ->
  datafrog_opt_loan_live_at Loan Point.
intros.
apply OriginLiveAxiom with Origin = Origin1 , Point = Point.
case H4.
search.
assert dead_borrow_region_can_reach_dead Origin1 Point Loan.
apply Lemma97 to H6 H2 H3.
search.


Theorem Lemma99:
  (
    forall Origin9,
    forall Loan,
    forall Point,
    my_origin_contains_loan_on_entry Origin9 Loan Point ->
    origin_live_on_entry Origin9 Point ->
    datafrog_opt_loan_live_at Loan Point
  ) /\ (
    forall Origin5,
    forall Origin9,
    forall Loan,
    forall Point,
    my_origin_contains_loan_on_entry Origin5 Loan Point ->
    my_subset Origin5 Origin9 Point ->
    ( origin_live_on_entry Origin5 Point -> false ) ->
    origin_live_on_entry Origin9 Point ->
    datafrog_opt_loan_live_at Loan Point
  ).
induction on 1 1.
split.
intros.
case H1.
search.
apply OriginLiveAxiom with Origin = Origin1 , Point = Point.
case H5.
apply IH to H3 H6.
search.
assert my_subset Origin1 Origin9 Point.
apply IH1 to H3 H7 H6 H2.
search.
intros.
case H1.
case H5.
apply Lemma98 to H6 H2 H4.
search.
search.
case H7.
search.
apply OriginLiveAxiom with Origin = Origin1 , Point = Point.
case H7.
apply IH to H5 H8.
search.
assert my_subset Origin1 Origin9 Point.
apply IH1 to H5 H9 H8 H4.
search.


Theorem Naive2DatafrogOpt:
  forall Loan,
  forall Point,
  naive_errors Loan Point ->
  datafrog_opt_errors Loan Point. 
intros.
case H1.
case H3.
apply OriginNaive2My to H4.
unfold. 
search.
case Lemma99.
apply H7 to H6 H5.
search.

